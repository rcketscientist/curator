repositories {
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

def versionMajor = 6
def versionMinor = 2
def versionPatch = 5

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
//        abortOnError false
    }

    def schemaLocation = "$projectDir/schemas".toString()

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                    "room.schemaLocation": schemaLocation,
                    "room.incremental": "true"
                ]
            }
        }

        // Specifies the fully-qualified class name of the test instrumentation runner.
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        androidTest.assets.srcDirs += files(schemaLocation)
    }

    signingConfigs {
        release {
            storeFile file("../key/my-release-key.keystore")
            storePassword "***REMOVED***"
            keyAlias "alias_name"
            keyPassword "***REMOVED***"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            debuggable true
        }
    }

    flavorDimensions "market", "cycle"

    productFlavors {
        // These fields handle flavor authorities for providers
        final FIELD_META = "PROVIDER_AUTHORITY_META"
        final FIELD_SWAP = "PROVIDER_AUTHORITY_SWAP"
        final PROVIDER_SWAP = "\"com.anthonymandra.rawdroid.SwapProvider\""
        final PROVIDER_SWAP_PRO = "\"com.anthonymandra.rawdroid.SwapProvider.pro\""
        demo {
            dimension "market"
            applicationId 'com.anthonymandra.rawdroid'
            buildConfigField "String", FIELD_SWAP, PROVIDER_SWAP
        }
        pro {
            dimension "market"
            applicationId 'com.anthonymandra.rawdroidpro'
            buildConfigField "String", FIELD_SWAP, PROVIDER_SWAP_PRO
        }
//        proAmazon {
//            dimension "market"
//            applicationId 'com.anthonymandra.rawdroidpro'
//            buildConfigField "String", FIELD_SWAP, PROVIDER_SWAP_PRO
//        }
        production {
            dimension "cycle"
        }
        beta {
            dimension "cycle"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
        // For ViewHolder binding https://blog.jetbrains.com/kotlin/2017/08/kotlin-1-1-4-is-out/
    }

    kotlinOptions {
        //https://stackoverflow.com/questions/59488983/why-i-still-get-cannot-inline-bytecode-built-with-jvm-target-1-8-into-bytecode
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation project(':showcaseView')
    implementation project(':inscription')
    implementation project(':metadata-extractor')
    implementation project(':rawprocessor')
    implementation project(':documentActivity')
    implementation project(':ssiv')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "androidx.core:core-ktx:$coreVersion"

    // UI
    implementation 'com.afollestad:material-cab:2.0.1'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.2.0'
    implementation 'com.eftimoff:android-viewpager-transformers:1.0.1@aar'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.github.bumptech.glide:compiler:4.11.0"

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0'

    // Material extension contains many of the necessary androidx classes
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.annotation:annotation:$annotationVersion"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Android architecture
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"
    implementation "androidx.work:work-runtime-ktx:$workVersion"
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Testing and Debug
    // Instrumented
    androidTestImplementation "androidx.arch.core:core-testing:$coreTestingVersion"
    androidTestImplementation "androidx.annotation:annotation:$annotationVersion"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.1'
    androidTestImplementation "androidx.work:work-testing:$workVersion"

    //    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'

    // Local
    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Debug
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.5'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-alpha-2'

    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.crashlytics.sdk.android:crashlytics-ndk:2.1.1'
}

crashlytics {
    enableNdk true
    androidNdkOut '../rawprocessor/library/build/intermediates/ndkBuild/debug/obj'
    androidNdkLibsOut '../rawprocessor/library/build/intermediates/ndkBuild/release/obj'
}
