buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
    maven { name 'glide-snapshot'; url 'http://oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
//apply plugin: 'maven'

def versionMajor = 5
def versionMinor = 6
def versionPatch = 4

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
//        abortOnError false
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25

        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"

//        jackOptions {
//            enabled true
//        }
    }

    dataBinding {
        enabled = true
    }

//    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
//    }

    signingConfigs {
        release {
            storeFile file("../key/my-release-key.keystore")
            storePassword "***REMOVED***"
            keyAlias "alias_name"
            keyPassword "***REMOVED***"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            jniDebuggable true
        }
    }

    flavorDimensions "market", "cycle"

    productFlavors {
        // These fields handle flavorized authorities for providers
        final FIELD_META = "PROVIDER_AUTHORITY_META"
        final FIELD_SWAP = "PROVIDER_AUTHORITY_SWAP"
        final FIELD_KEYWORD = "PROVIDER_AUTHORITY_KEYWORD"
        final PROVIDER_META = "\"com.anthonymandra.content.MetaProvider\""
        final PROVIDER_SWAP = "\"com.anthonymandra.rawdroid.SwapProvider\""
        final PROVIDER_KEYWORD = "\"com.anthonymandra.content.KeywordProvider\""
        final PROVIDER_META_PRO = "\"com.anthonymandra.content.MetaProvider.pro\""
        final PROVIDER_SWAP_PRO = "\"com.anthonymandra.rawdroid.SwapProvider.pro\""
        final PROVIDER_KEYWORD_PRO = "\"com.anthonymandra.rawdroid.KeywordProvider.pro\""
        demo {
            dimension "market"
            applicationId 'com.anthonymandra.rawdroid'
            buildConfigField "String", FIELD_META, PROVIDER_META
            buildConfigField "String", FIELD_SWAP, PROVIDER_SWAP
            buildConfigField "String", FIELD_KEYWORD, PROVIDER_KEYWORD
        }
        pro {
            dimension "market"
            applicationId 'com.anthonymandra.rawdroidpro'
            buildConfigField "String", FIELD_META, PROVIDER_META_PRO
            buildConfigField "String", FIELD_SWAP, PROVIDER_SWAP_PRO
            buildConfigField "String", FIELD_KEYWORD, PROVIDER_KEYWORD_PRO
        }
//        proAmazon {
//            dimension "market"
//            applicationId 'com.anthonymandra.rawdroidpro'
//            buildConfigField "String", FIELD_META, PROVIDER_META_PRO
//            buildConfigField "String", FIELD_SWAP, PROVIDER_SWAP_PRO
//            buildConfigField "String", FIELD_KEYWORD, PROVIDER_KEYWORD_PRO
//        }
        production {
            dimension "cycle"
        }
        beta {
            dimension "cycle"
        }
    }
}

dependencies {
    implementation project(':showcaseView')
    implementation project(':inscription')
    implementation project(':metadata-extractor')
    implementation project(':rawprocessor')
    implementation project(':documentActivity')
    implementation project(':toggleButtons')
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
    implementation('com.crashlytics.sdk.android:crashlytics-ndk:1.1.6@aar') {
        transitive = true;
    }

//    implementation 'com.anthonymandra:ToggleButtons:2.0.0'

    //includes support-v4 and appcompat-v7
    implementation "com.android.support:design:${rootProject.ext.supportLibVersion}"
    implementation "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
    implementation 'android.arch.lifecycle:extensions:' + rootProject.archLifecycleVersion
    implementation 'android.arch.persistence.room:runtime:' + rootProject.archRoomVersion
    kapt 'android.arch.lifecycle:compiler:' + rootProject.archLifecycleVersion
    kapt 'android.arch.persistence.room:compiler:' + rootProject.archRoomVersion

    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'

    implementation 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation "android.arch.lifecycle:common-java8:1.0.0-beta1"
}

crashlytics {
    enableNdk true
    androidNdkOut       '../rawprocessor/library/build/intermediates/ndkBuild/debug/obj'
    androidNdkLibsOut   '../rawprocessor/library/build/intermediates/ndkBuild/release/obj'
}